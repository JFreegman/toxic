load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("//tools/project:build_defs.bzl", "project")

project(license = "gpl3-https")

AV_SRCS = [
    "src/audio*.c",
    "src/video*.c",
    "src/x11*.c",
]

COPTS = [
    "-D_DEFAULT_SOURCE",
    "-DGAMES",
    "-DHAVE_WIDECHAR",
    "-DPACKAGE_DATADIR='\"data\"'",
    "-DQRCODE",
] + select({
    "//tools/config:linux-x86_64": [
        "-DAUDIO",
        "-DVIDEO",
        "-DPYTHON",
    ],
    "//conditions:default": [],
})

cc_library(
    name = "libtoxic",
    srcs = glob(
        [
            "src/*.c",
            "src/*.h",
        ],
        exclude = ["src/main.c"] + AV_SRCS,
    ) + select({
        "//tools/config:linux-x86_64": glob(AV_SRCS),
        "//conditions:default": [],
    }),
    hdrs = glob(["src/*.h"]),
    copts = COPTS,
    tags = ["no-windows"],
    deps = [
        "//c-toxcore",
        "@curl",
        "@libconfig",
        "@libqrencode",
        "@ncurses",
    ] + select({
        "//tools/config:linux-x86_64": [
            "@libvpx",
            "@openal",
            "@python3//:python",
            "@x11",
            "@xproto",
        ],
        "//conditions:default": [],
    }),
)

cc_binary(
    name = "toxic",
    srcs = ["src/main.c"],
    copts = COPTS,
    tags = ["no-windows"],
    deps = [
        ":libtoxic",
        "//c-toxcore",
        "@curl",
        "@libconfig",
        "@libqrencode",
        "@ncurses",
    ] + select({
        "//tools/config:linux-x86_64": [
            "@libvpx",
            "@openal",
            "@python3//:python",
            "@x11",
        ],
        "//conditions:default": [],
    }),
)

sh_test(
    name = "toxic_test",
    size = "small",
    srcs = [":toxic"],
    args = ["--help"],
    tags = ["no-windows"],
)

cc_test(
    name = "line_info_test",
    size = "small",
    srcs = ["src/line_info_test.cc"],
    tags = ["no-windows"],
    deps = [
        ":libtoxic",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "misc_tools_test",
    size = "small",
    srcs = ["src/misc_tools_test.cc"],
    tags = ["no-windows"],
    deps = [
        ":libtoxic",
        "@com_google_googletest//:gtest",
        "@com_google_googletest//:gtest_main",
    ],
)
